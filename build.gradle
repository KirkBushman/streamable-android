// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

    ext {
        android_gradle_ver = '4.0.1'

        compile_sdk_ver = 30
        build_tools_ver = "30.0.2"

        min_sdk_ver = 21
        target_sdk_ver = 30

        jvm_ver = JavaVersion.VERSION_1_8

        kotlin_ver     = "1.4.10"
        coroutines_ver = '1.3.9'
        ktx_ver        = '1.3.1'
        ktlint_ver     = '0.39.0'
        constr_ver     = '2.0.1'
        retrofit_ver   = '2.9.0'
        moshi_ver      = '1.10.0'
        okhttp_ver     = '4.9.0'
        mdc_ver        = '1.2.1'
        epoxy_ver      = '4.1.0'

        test_ver       = '1.3.0'
        espresso_ver   = '3.3.0'
        junit_ver      = '1.1.2'
        leak_ver       = '2.4'
    }

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$android_gradle_ver"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_ver"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {

    // ktlint
    apply plugin: 'checkstyle'
    apply from: "${project.rootDir}/ktlint.gradle"

    task checkstyle(type: Checkstyle) {
        configFile rootProject.file('checkstyle.xml')
        source 'src/main/java'
        ignoreFailures false
        showViolations true
        include '**/*.java'

        classpath = files()
    }

    afterEvaluate {
        if (project.tasks.findByName('check')) {
            check.dependsOn('checkstyle')
            check.dependsOn('ktlint')
        }

        if (project.tasks.findByName('dokka')) {
            check.dependsOn('dokka')
        }
    }
}

task clean (type: Delete) {
    delete rootProject.buildDir
}